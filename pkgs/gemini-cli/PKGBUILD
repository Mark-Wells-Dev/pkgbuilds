# Maintainer: Mark Wells <contact at markwells dot dev>
# Packaged from npm
pkgname=gemini-cli
pkgver=0.27.2
pkgrel=1
epoch=1
pkgdesc="Open-source AI agent that brings the power of Gemini directly into your terminal"
arch=('any')
url="https://github.com/google-gemini/gemini-cli"
license=('Apache-2.0')
depends=('nodejs')
makedepends=('npm')
options=('!strip' '!debug' '!lto')
source=("https://registry.npmjs.org/@google/gemini-cli/-/${pkgname}-${pkgver}.tgz"
    "gemini-wrapper.sh")
noextract=("${pkgname}-${pkgver}.tgz")
sha512sums=('9c194327b87afd87a093449158e7c4cad30fb8ff8bdce20468a1ed3546ebec73f9677b4d10928f4581f1ad704c29a29bf991148b254508363663ee4539449be8' 'cf83e1357eefb8bdf1542850d66d8007d620e4050b5715dc83f4a921d36ce9ce47d0d13c5d85f2b0ff8318d2877eec2f63b931bd47417a81a538327af927da3e')

package() {
    npm install -g --prefix "${pkgdir}/usr" \
        --cache "${srcdir}/npm-cache" \
        --silent --no-fund --no-audit --no-update-notifier \
        "${srcdir}/${pkgname}-${pkgver}.tgz"

    # Fix permissions
    find "${pkgdir}/usr" -type d -exec chmod 755 {} +

    # Clean npm metadata from package.json files
    find "${pkgdir}" -type f -name package.json -print0 \
        | xargs -0 sed -i -e '/_where/d' -e '/_args/d' -e '/_from/d' -e '/_resolved/d'

    # Remove build artifacts
    find "${pkgdir}" -name config.gypi -delete
    find "${pkgdir}" -name Makefile -delete

    # License
    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
    if [ -f "${pkgdir}/usr/lib/node_modules/@google/gemini-cli/LICENSE" ]; then
        ln -s "../../../lib/node_modules/@google/gemini-cli/LICENSE" \
            "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    fi

    # Replace symlink with wrapper to suppress punycode deprecation warnings (Node 21+)
    if [ -L "${pkgdir}/usr/bin/gemini" ]; then
        rm "${pkgdir}/usr/bin/gemini"
    fi
    install -Dm755 "${srcdir}/gemini-wrapper.sh" "${pkgdir}/usr/bin/gemini"
}